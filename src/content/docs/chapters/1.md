---
title: Chapter 1 - Humble Beginnings
description: A first look into nix and NixOS, create a VM to experiment in
---

Welcome! This guide is my take on trying to teach the wonders of Nix and NixOS from scratch.
This will comprise of many parts, and we'll only *install* the OS later on. This guide prioritizes getting a safe environment for you to learn in over getting NixOS installed fully right away.

I feel many mistakes people make with Nix and NixOS is jumping right in without understanding that Nix/NixOS **has a fundamentally different approach** to many things. Many inuitions you built as a programmer and Linux user may not work the same here.

This guide will assume you're coming from another Linux distro, and that you have a background in programming (Nix is a programming language as well, you'll need to know a bit of functional programming to understand what's happening).

In this chapter we will...

1. Install Nix on your current distro
2. Go over using the basic parts of the Nix CLI
3. Set up a basic NixOS VM with Nix itself
4. Go over the basics of configuring and installing on a NixOS system

Ready?

## Installing Nix

Before we even start with NixOS, we're going to use `Nix` to build a VM we can use to test with.
This VM will eventually be the system you'll install

We'll *actually* be using a fork of Nix called Lix. This fork fixes some common bugs and turns on some features we'll be using by default.

To install Lix, follow [the Lix installation guide](https://lix.systems/install/#on-any-other-linuxmacos-system). After installing check that you can run Nix by running `nix --version`.

## Basic Nix CLI

Nix is a package manager that can run on many systems. One of the nice things it can do is run a package without needing to install it. For example:

```sh
nix run "nixpkgs#python3"
```

We can run Python!

```sh
nix shell "nixpkgs#python3"
```

And we can run a shell with Python in scope!

Here we're asking for `python3` from `nixpkgs` (the primary package repository). In the first command we simply run the package's default binary. In the second we bring the package into scope, meaning we can execute any commands it provides.

We'll get into more later, just keep in mind we can run any package in [nixpkgs](https://search.nixos.org/packages?channel=unstable&size=500)
like this!

## Now let's make a VM

To start out make a new folder to contain this project. I'd recommend somewhere easy to access via terminal. In this new folder run `nix flake init`. A flake is like a Nix "project". Opening the `flake.nix` we can see `inputs`, which is like the dependencies of our project. And we can also see outputs, flakes can output many things from packages (as it does now) to NixOS configurations (what we'll do in a bit), and much much more.

### Optional: Editor Extension

I'd recommend grabbing `bbenoist.Nix` for VSCode for editing Nix files.

### Getting flake.nix set up

- Nix is a functional programming language as well as a package manager
- We'll go over some basics of Nix expressions.
- This entire `flake.nix` is a nix expression, it's an *attr set* (kinda like a dictionary or hash map) with three attributes
- `description` is a string, we can make this anything we want
- `inputs` is another attr set, it tells Nix what inputs (dependencies) our flake ("project") has
- `outputs` is a function that takes `self` and `nixpkgs`. `self` is a reference to this flake, `nixpkgs` is the input we defined above
- `outputs` returns an attr set of well... outputs
- In this example we're outputting an attr set called `packages` with another attr set for the `x86_64-linux` system. One named `hello` and the other named `default`
- Default is special, it's basically the "main" package of the flake
- The `packages.x86_64-linux.hello = nixpkgs.legacyPackages.x86_64-linux.hello;` syntax is equivalent to
- `packages = { x86_64-linux = { hello = nixpkgs.legacyPackages.x86_64-linux.hello; }; };`
- Nix provides the shorter syntax to make it easier for us to define nested attr sets
- You may notice that we use the same syntax for the `default` package, Nix merges these two statements into one attr set for us!

### Writing Our NixOS Config

- Clear the two lines adding the `hello` and `default` packages
- Add this before the actual attr set

```nix
outputs = {
    self,
    nixpkgs,
  }: let
    system = "x86_64-linux";
    pkgs = import nixpkgs {
      inherit system;
    };
  in {
    
  };
```

- We can see a new keyword, `let`. This *lets* us define "variables" we can insert in the following block
- Here we're defining two variables, `system` (a string), and `pkgs` (attr set).
- Note the syntax, we need a `;` after every definition, and we need the word `in` before the actual block
- For `pkgs` we're importing nixpkgs, we won't go into exactly what this means but think of it like configuring it how we want
- We're then inheritting system. This is equivalent to `system = system;`. `system` is a variable we defined above
- The `inherit` keyword is yet another shortcut nix provides for when the attribute you want to set is named the same as the variable it should be set to.
- So what we've just done is brought nixpkgs, configured for `x86_64-linux`, into scope. This is the system our NixOS config will be built for

Now onto the actual body of the attr set.

- Add a new line in the attr set, `legacyPackages.${system} = pkgs;`
- This is for compatibility, we'll need it later.
- The `${}` syntax lets us insert a variable when using dots, this statement is equivalent to `legacyPackages.x86_64-linux = pkgs;`

Now we'll make the system.

- Add another new line, `nixosConfigurations.YOURNAMEHERE = nixpkgs.lib.nixosSystem {` (will be continued next line)
- Replace "YOUNAMEHERE" with whatever name you want for your system, this will set the system's network host name as well
- We're now outputting a NixOS configuration with with the name you specified (this means it's possible to have one flake for multiple systems :D)
- `nixpkgs.lib.nixosSystem` is a *function* that we're calling with one argument (an attr set that we'll write on the next few lines)
- Function calling in Nix is done with spaces similar to other functional languages

Ok now let's set some options for our system

- New line, `inherit system pkgs;`, We're telling Nix we want the config to be for `x86_64-linux` and it should use the packages we just defined
- Another new line, `time.timeZone = "America/New_York";`, change to wtv timezone you want (can always change it later!)
- Next line, `modules = [./config.nix];`
- Last line, end our attr set definition: `};`

Wait!

- So we're referencing a file in the current directory called `config.nix`, we'll need to make this
- Note the lack of quotes around the path, this means something special to Nix, we won't go into it but it's not simply a string
- Create `config.nix`
- IMPORTANT: If you made a git repo for this, run `git add .`; Nix only sees files in Git
- In this file we'll defining a *NixOS Module*, this is a file that defines our config for the system.
- Since `modules` is an array, you can define multiple, in the future you may want to consider this for organization as you'll configure *a lot*
- A NixOS module should be a *function* that takes a few arguments (we'll use these later) and outputs an *attr set* that represents part of a NixOS config
- So, to define this we write

```nix
{pkgs, ...}: {
   
}
```

- First our function takes an argument called `pkgs` this is a reference to the packages we inherited in `flake.nix`. We use this whenever we want to reference a package within `nixpkgs`.
- The `...` in the arguments means we'll ignore any additional arguments passed
- Without this we'll get an error as NixOS will pass arguments we don't expect
- We'll add arguments as needed later on

Now we need to write in between the second set of braces to define our config

- First line in the block, `system.stateVersion = "24.11";`.
- New line, `time.timeZone = "America/New_York";`. Sets system time zone to wtv we want
- New line, Add:

```nix
users.users.YOURNAME = {
  isNormalUser = true;
  password = "asdf";
  extraGroups = ["networkmanager" "wheel"];
};
```

- Replace `YOURNAME` with the name you want your user to have
- Makes you a normal user
- Sets your password to `asdf`. WARNING: Don't make this your actual password yet. We're setting it here for simplicity. Ideally keep it as smth like asdf
- Adds you to the network manager group so you can configure network connections if needed
- Adds you to the `wheel` group, effectively makes you a sudoer
- Notice that in arrays Nix doesn't make us put commas, just put a space! (Still deciding if I like this or not)

We now have a very basic config set up, let's boot it!

## Running our System

- If you decide to keep going down the Nix/NixOS rabbit hole you'll find that Nix gives you super powers
- One of these super powers includes easy and simple VM building
- In the current directory **type** 

```sh
nix run .#nixosConfigurations.YOURNAME.config.system.build.vm
```

- You'll need to replace "YOURNAME" with *the name of your system config you've defined in flake.nix*
- Here we're using the `nix run` command just like we did at the start with Python
- Except now instead of using the `nixpkgs` flake we're using the current directory's flake (`.`). The `#` part means we're getting an output of this flake
- We're saying we want to *run* `nixosConfigurations.YOURNAME.config.system.build.vm` which is a special package NixOS exports that builds and runs a VM of the system's config.
- Now go ahead and run it! (you may need to put it in quotes btw)

### ...

- I'll assume everything went well
- Login to your system with the username and password you configured
- Explore! You'll notice it's a very minimal system, NixOS doesn't come with much by default, but that's the point

## Customization

So let's get the system in a more usable state

- First why don't we get a DE in here. I'm going to do KDE but you can find out the options needed to enable others by googling and checking the NixOS wiki
- We'll place these options after the `users` block in `config.nix`
- For KDE we need the following

```nix
services.xserver.enable = true;
services.displayManager.sddm.enable = true;
services.desktopManager.plasma6.enable = true;
```

- Remember that the dot notation is simply a fancy way to set nested attr sets. Here we're enabling XServer, SDDM, and Plasma 6.
- No matter what DE you'll use, I recommend setting `services.qemuGuest.enable = true;` for now, as that'll install the required QEMU guest agent
- Now start the VM again with the same command (you may need to delete the existing `nixos.qcow2` file)
- Note if you're experience performance issue you may need to dedicate more memory to the VM, set the `QEMU_OPTS` to `-m 4G`, adjust the 4 gigs as needed

## Configuring Nix with Nix

Now we're going to do some config of Nix itself within the VM. This won't affect the Nix that you're using to run the VM

- Start off a new line `nix = {`, we're gonna be doing a lot of options here so makes sense to do a full attr set syntax
- Within the block do `channel.enable = false;` This'll disable Nix channels, they're what preceded flakes and we really don't need them
- Next do `package = pkgs.lix;`, similar to how we installed Lix on your current distro, we're going to switch out the implementation of Nix in our VM for it as well
- Now within the nix block do `settings = {`, yet another full attr set
- In `settings` set `auto-allocate-uids`, `use-cgroups`, and `auto-optimise-store` to `true`
- Now make a new array called `experimental-features` and in it put the strings `nix-command`, `flakes`, `ca-derivations`, `auto-allocate-uids`, and `cgroups`.
- Finish up the `settings` block with `};`
- Now make *another* attr set called `gc` after `settings` with `automatic` set to `true` and `dates` set to the string `weekly`

At the end it should look like this

```nix
nix = {
  channel.enable = false;
  package = pkgs.lix;
  settings = {
    experimental-features = [
      "nix-command"
      "flakes"
      "ca-derivations"
      "auto-allocate-uids"
      "cgroups"
    ];
    auto-allocate-uids = true;
    use-cgroups = true;
    auto-optimise-store = true;
  };
  gc = {
    automatic = true;
    dates = "weekly";
  };
};
```

- From now on I'll just be showing code blocks to represent what to do
- I'll explain each part of course but the intention of the syntax should be easy to parse by now hopefully
- Any new syntax item's I'll also explain.

## Installing Programs

### Method 1

This should be the first method you reach for, you can use [NixOS Option Search](https://search.nixos.org/options?channel=unstable)
to find if there's an option for your program. For example, for Firefox I can do:

```nix
programs.firefox.enable = true;
```

Firefox will then be pulled from nixpkgs and installed into our VM. We can also configure other aspects of Firefox with NixOS.
I won't get into it but most programs that are installed with this method have additional config you can set via Nix

### Method 2

If we can't find our program through NixOS options search, we'll need to add the package for it to `environment.systemPackages`.
This option is an array of *packages*. You can check it's definition using the link to NixOS Options Search above!

To find packages we can use the [Nix packages search](https://search.nixos.org/packages?channel=unstable&size=500) - use that link or
you can just click "Packages" at the top of the NixOS options search (they're the same website)

Before we can actually add packages to our system, we need to get a reference to nixpkgs inside our config. To do this, we can add `pkgs`
to the list of arguments we accept at the top, so it should look like `{pkgs, ...}: {`. Make sure to keep the `...`!

For example, let's say I want `nix-output-monitor` (a very nice program for making build output nicer if you do more with Nix),
I'd add it to `environment.systemPackages` like so:

```nix
environment.systemPackages = with pkgs; [
  nix-output-monitor
];
```

New keyword alert! The `with` keyword brings all attributes of a given attr set into scope for the block it's on. Without it we'd need to
say `pkgs.nix-output-monitor`. This keyword is very handy for when we don't want to prefix everything with `pkgs.`

## Experiment

From here I'd recommend trying out programs, options, etc. You now know the basics of configuring NixOS!

However, there's much you must learn before you can install and daily drive. The following guides will go through some more advanced concepts. Stay tuned!
