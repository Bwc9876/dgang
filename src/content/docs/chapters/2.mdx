---
title: Chapter 2 - Housekeeping
description: Boring stuff time! Setting up your config to be easier to edit and maintain later
---

import { FileTree, Aside, Steps } from '@astrojs/starlight/components';

So far we've been working in one file with no VCS setup and no consistent formatting.

In this chapter we will...

1. Get a Git repo set up for our system
2. Learn how to compartmentalize our config into different files
3. Do some setup for later chapters in `flake.nix`
4. Setup auto-formatting for our code

## Setting Up Git

Before we do anything, we'll need to make a `.gitignore`.

<Steps>
1. Create a `.gitignore` file to prevent unwanted files from being checked-in to VCS.
    <FileTree>
    - flake.nix
    - **.gitignore**
    - ...
    </FileTree>
2. I'd recommend this as a `.gitignore`.
    ```txt title=".gitignore"
    # Symlink to anything nix built (no trailing slash)
    result 
    # The VM image file QEMU creates for our VM
    nixos.qcow2
    ```
3. Now initialize a new Git repo for your project.
    ```sh
    git init
    git add .
    git commit -m "Initial Commit"
    ```
</Steps>

We now have a git repo for our config!

<Aside title="From Now On..." type="caution">
Whenever we *create or rename* a file in our repo, we need to add it in git for Nix to see it. 
```sh
git add .
```
If you ever get an error saying a file in the config could not be found, this is likely the cause.
</Aside>

## Fragmenting Our Config

Now we'll split up our NixOS config from one file, this is for organization.

<Steps>
1. Create files for each part of our config.
    To keep things clean, you probably want to put these in a folder called `modules`. **flake.nix should not go in this folder, flake files must be in the root of the repo.**

    Anything highlighted is something this guide is going to use later, so they'll be required. Anything not highlighted is a suggestion based on how others tend to organize files, but pick what works best for you!

    <FileTree>
    - flake.nix
    - flake.lock
    - modules/
      - **config.nix** Sets up basics and imports all other modules
      - **nix.nix** Configures Nix itself within the VM
      - graphics.nix (DEs, apps, theming, etc)
      - shell.nix (Aliases, themes, etc)
      - ... Go wild!
    </FileTree>

    <Aside type="tip" title="Pop Quiz!">We just made files! What do we do to make sure Nix can see them?</Aside>
2. You'll also need to edit the path specified in `flake.nix` within the `modules` array to point to `./modules/config.nix` instead.
    ```nix title="flake.nix" "YOURNAME" ins="/modules"
    {
      # ...
      nixosConfigurations.YOURNAME = nixpkgs.lib.nixosSystem {
        # ...
        modules = [
          ./modules/config.nix
        ];
      };
    }
    ```
</Steps>

### Importing other modules from config.nix

Now we need to tell NixOS what other files to load modules from.

To do this, in `config.nix` create a new array called `imports` (usually this is the very first config option).

This array should have the paths (relative to `config.nix`) of other modules to load.

```nix title="modules/config.nix" ins={2}
{pkgs, ...}: {
  imports = [./nix.nix ./graphics.nix ./shell.nix];

  # ...
}
```

### File Contents

Now fill in the modules we've created, use config.nix as reference, remember each module must return an attr set and are given a bunch of arguments. Here's a good template for modules.

```nix title="modules/some-module.nix"
{pkgs, ...}: {

}
```

From there feel free to organize your source tree however you want!

<Aside type="tip">
Remember that Nix/NixOS merges a lot of stuff! For example you can define `environment.systemPackages` in multiple files and it will aggregate all the items into one big array.

If two things in different modules collide, Nix will fail to evaluate and let you know. Don't be scared to experiment!
</Aside>

### Making nix.nix

I'll leave exactly where to place certain options up to you, however this guide is going to require you to place the `nix = { ... };` option inside `modules/nix.nix`, we'll be importing it from somewhere else later.

<Steps>
1. Remove the `nix` attr set from `modules/config.nix`.
    ```nix title="modules/config.nix" del={3-17}
    {pkgs, ...}: {
      # ...
      nix = {
        channel.enable = false;
        package = pkgs.lix;
        settings = {
          experimental-features = [
            "nix-command"
            "flakes"
          ];
          auto-optimise-store = true;
        };
        gc = {
          automatic = true;
          dates = "weekly";
        };
      };
      # ...
    }
    ```
2. Then add the attr set in `modules/nix.nix`.
    ```nix title="modules/nix.nix" ins={2-16}
    {pkgs, ...}: {
      nix = {
        channel.enable = false;
        package = pkgs.lix;
        settings = {
          experimental-features = [
            "nix-command"
            "flakes"
          ];
          auto-optimise-store = true;
        };
        gc = {
          automatic = true;
          dates = "weekly";
        };
      };
    }
    ```
</Steps>

<Aside title="Try Building">
Before continuing, make sure your config still builds and your VM is how you want it.
</Aside>

## Passing inputs to our configuration

So when I said there were only two methods to install software I kinda lied...

Nix flakes are a wonderful way to get software straight from the repo the software is hosted on. We can see in our `flake.nix` that we're grabbing `nixpkgs` from GitHub. 

```nix title="flake.nix" {3}
{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
  };

  # ...
}
```

Since NixOS uses nixpkgs already, it's already passed to us as an argument to modules. However, there may come a time that we need to access other inputs within our modules. 

In order to do this we have to tell NixOS additional arguments we want passed.

<Steps>
1. On the line with `outputs`, add `inputs @` before the `{`. 
    ```nix title="flake.nix" ins="inputs @ "
    outputs = inputs @ {
        self,
        nixpkgs
    }: let
    ```
    <Aside>
    The `@` is called the "at" pattern.
    This makes a new variable called `inputs` available that contains all of our flake inputs. 
    It allows us to have one large attr set with all of our inputs, while also having each of them in scope individually.
    Another possible way to do this without an `@` would be with `with`.
    ```nix
    inputs: with inputs; {
      # ...
    }
    ```
    We often prefer `@` so we can explicitly see what arguments are expected.
    </Aside>
2. Create a new attr set in our call to `pkgs.lib.nixosSystem` called `specialArgs`, we'll want to `inherit` our `inputs` here.
    ```nix title="flake.nix" ins={3} "YOURNAME"
    nixosConfigurations.YOURNAME = pkgs.lib.nixosSystem {
      # ...
      specialArgs = { inherit inputs; };
      modules = [
        # ...
      ];
    }
    ```
3. Now when we need to, we can specify `inputs` in our arguments on any module and have access to our flake inputs.
    ```nix title="modules/some-module.nix" ins="inputs, "
    {inputs, pkgs, ...}: {
      # Access to flake inputs! Yay!
    }
    ```
</Steps>

This may not help much as our only inputs at the moment are `self` and `nixpkgs`, but as we add more this will come in handy.

## Auto-formatting

This step is optional but I'd recommend it if you're like me and enjoy consistent style in your code.

Another type of output a nix flake can provide is a `formatter`. This should be set to a package that has the ability to format Nix code, I'll use [alejandra](https://search.nixos.org/packages?channel=unstable&show=alejandra&from=0&size=500&sort=relevance&type=packages&query=alejandra) (my personal preference) in this example.

To setup a formatter simply add it as an output to our flake.

```nix title="flake.nix" ins={5}
outputs = inputs @ {
  self,
  nixpkgs
}: ... {
  formatter.${system} = pkgs.alejandra;

  # ...
}
```

Now we can format our code with Alejandra.

```sh
nix fmt
```

<Aside type="tip">
There's also a VSCode extension, `kamadorueda.alejandra`, that can allow VSCode to run formatting for you. You may need to install alejandra to your host system fully for it to work.
```sh
nix profile install nixpkgs#alejandra
```
</Aside>
